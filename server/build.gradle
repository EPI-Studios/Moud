plugins {
    id 'java'
    id 'com.gradleup.shadow' version "8.3.5"
    id 'com.moud.logging-lint'
}

group = 'com.moud'
version = rootProject.file('VERSION').text.trim()

repositories {
    mavenCentral()
    maven {
        url = "https://repo.minestom.com/"
    }
    maven {
        url = "https://repo.smolder.cloud/public/"
    }
}

dependencies {
    implementation project(':api')
    implementation project(':plugin-api')
    implementation project(':network-engine')

    implementation("fr.ghostrider584:axiom-minestom:0.0.2")

    implementation('net.minestom:minestom-snapshots:f966351cb7') {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j2-impl'
    }
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'org.graalvm.polyglot:polyglot:24.1.0'
    implementation 'org.graalvm.polyglot:js:24.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.0'
    implementation 'io.github.classgraph:classgraph:4.8.172'
    implementation 'org.apache.commons:commons-exec:1.4.0'
    implementation("com.github.stephengold:jolt-jni-Linux64:3.5.0")
    runtimeOnly("com.github.stephengold:jolt-jni-Linux64:3.5.0:DebugSp")
    runtimeOnly("com.github.stephengold:jolt-jni-Linux_ARM64:3.5.0:DebugSp")
    runtimeOnly("com.github.stephengold:jolt-jni-Windows64:3.5.0:DebugSp")
    implementation("io.github.electrostat-lab:snaploader:1.1.1-stable")

    def javafxVersion = '21.0.1'
    def os = org.gradle.internal.os.OperatingSystem.current()
    def javafxPlatform = os.isWindows() ? 'win' : (os.isMacOsX() ? 'mac' : 'linux')
    implementation "org.openjfx:javafx-base:${javafxVersion}:${javafxPlatform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${javafxPlatform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${javafxPlatform}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}:${javafxPlatform}"

    implementation 'org.yaml:snakeyaml:2.3'


}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jar {
    manifest {
        attributes["Main-Class"] = "com.moud.App"
    }
}

shadowJar {
    mergeServiceFiles()
    archiveBaseName.set("moud-server")
    archiveClassifier.set("")
    archiveVersion.set("")
    manifest {
        attributes["Main-Class"] = "com.moud.App"
    }
}

tasks.register('generateTsMetadata', JavaExec) {
    group = 'moud'
    description = 'Generates TypeScript metadata JSON for the SDK.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.moud.server.ts.TsMetadataGenerator'
    args "${rootProject.projectDir}/packages/sdk/generated/server-api.json"
}


build {
    dependsOn(shadowJar)
}
